import groovy.json.JsonSlurper

apply plugin: 'com.android.library'

/**
 * Load ExoDoris version from package.json file
 */
def packageJsonFile = file('../package.json')
def packageJson = new JsonSlurper().parseText(packageJsonFile.text)
def dorisAndroidVersion = packageJson.dorisAndroidVersion

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet('compileSdkVersion', 31)
    buildToolsVersion safeExtGet('buildToolsVersion', '29.0.3')

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 22)
        targetSdkVersion safeExtGet('targetSdkVersion', 29)
        versionCode 1
        versionName "1.0"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'com.facebook.react:react-native:+'

    // Amazon Publishing Services
    implementation project(":AmazonFireTVAdsSDK-release")

    // ExoDoris
    api ("com.github.DiceTechnology.doris-android:doris-ui:$dorisAndroidVersion"){
        exclude group: 'com.github.DiceTechnology', module: 'dice-shield-android'
        exclude group: "androidx.media3"
    }
    api ("com.github.DiceTechnology.doris-android:doris-custom-ui:$dorisAndroidVersion"){
        exclude group: 'com.github.DiceTechnology', module: 'dice-shield-android'
        exclude group: "androidx.media3"
    }

    // Stetho
    implementation 'com.facebook.stetho:stetho-okhttp3:1.6.0'

    // Google Guava
    implementation "com.google.guava:guava:30.1-android"

    // Google Gson
    implementation "com.google.code.gson:gson:2.8.6"

    // All support libs must use the same version
    implementation "androidx.annotation:annotation:1.0.0"
    implementation "androidx.appcompat:appcompat:1.0.0"
    implementation "androidx.media:media:1.0.0"

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation(project(':dice-shield-android')) {
        exclude group: "androidx.media3", module: "media3-exoplayer"
        exclude group: "androidx.media3", module: "media3-exoplayer-dash"
        exclude group: "androidx.media3", module: "media3-ui"
    }

    implementation 'com.github.bumptech.glide:glide:4.13.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.13.0'
}
